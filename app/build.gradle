buildscript {
    repositories {
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}

apply plugin: 'build-announcements'
apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.jakewharton.hugo'

def gitVersion() {
    return "git rev-list master --first-parent --count".execute([], project.rootDir).text.toInteger()
}

def gitTag() {
    return "git describe --tags".execute([], project.rootDir).text.trim()
}

retrolambda {
    javaVersion JavaVersion.VERSION_1_7
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "io.romain.passport"
        minSdkVersion 21
        targetSdkVersion 23

        versionCode gitVersion()
        versionName gitTag()

        buildConfigField "String", "GCM_SENDER_ID", "\"${gcm_sender_id}\""
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // Change the name of the apk.
            archivesBaseName = "passport-${defaultConfig.versionName}"
        }

        lintOptions {
            checkReleaseBuilds false
            abortOnError false
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    support = '23.1.1'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:support-v4:${support}"
    compile "com.android.support:design:${support}"
    compile "com.android.support:appcompat-v7:${support}"
    compile "com.android.support:cardview-v7:${support}"
    compile "com.android.support:recyclerview-v7:${support}"
    compile "com.android.support:palette-v7:${support}"

    compile 'com.google.android.gms:play-services-gcm:8.+'
    compile 'com.google.android.gms:play-services-location:8.+'

    // Dagger
    compile 'com.google.dagger:dagger:2.+'
    apt 'com.google.dagger:dagger-compiler:2.+'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    // Butterknife
    compile 'com.jakewharton:butterknife:8.+'
    apt 'com.jakewharton:butterknife-compiler:8.+'

    // GSON
    compile 'com.google.code.gson:gson:+'

    // OkHttp
    compile 'com.squareup.okhttp:okhttp:2.+'
    compile 'com.squareup.okhttp:logging-interceptor:2.+'

    // RxJava
    compile('io.reactivex:rxandroid:+') {
        exclude module: 'rxjava'
    }
    compile 'io.reactivex:rxjava:1.+'

    // Retrofit
    compile('com.squareup.retrofit:retrofit:2.+') {
        exclude module: 'okhttp'
    }
    compile 'com.squareup.retrofit:converter-gson:2.+'
    compile 'com.squareup.retrofit:adapter-rxjava:2.+'

    // Glide
    compile 'com.github.bumptech.glide:glide:3.6.+'
//    compile 'com.github.bumptech.glide:okhttp-integration:+'

    // Database Schematic
    apt 'net.simonvt.schematic:schematic-compiler:0.6.4'
    compile 'net.simonvt.schematic:schematic:0.6.4'

    // SVG Parser
    compile 'com.caverock:androidsvg:+'
//
//    // Auto Parcel
//    apt 'com.github.frankiesardo:auto-parcel-processor:+'
//    compile 'com.github.frankiesardo:auto-parcel:+'
}
